/*
 *
 *  Copyright 2017 Netflix, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://repo.spring.io/milestone"
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
        classpath("com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1")
        classpath("com.netflix.nebula:gradle-netflixoss-project-plugin:7.1.3")
        classpath("gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.6")
    }
}


plugins {
    id "com.github.kt3k.coveralls" version "2.8.1"
    id "org.ajoberstar.github-pages" version "1.7.2"
    id "org.unbroken-dome.test-sets" version "2.1.1"
    id "io.franzbecker.gradle-lombok" version "3.0.0"
}

apply plugin: "nebula-aggregate-javadocs"

ext {
    githubProjectName = rootProject.name
}

def javaProjects = subprojects.findAll {
    it.name != "metacat-demo" && it.name != "metacat-ddl" && it.name != "metacat-docs"
}

allprojects {
    apply plugin: "jacoco"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "nebula.netflixoss"
    apply plugin: "io.franzbecker.gradle-lombok"

    repositories {
        jcenter()
        maven {
            url "http://repo.spring.io/milestone"
        }
        maven {
            url "http://redshift-maven-repository.s3-website-us-east-1.amazonaws.com/release"
        }
        maven {
            url 'https://jitpack.io'
        }
    }
}

idea {
    project {
        jdkName = "1.8"
        languageLevel = "1.8"
        vcs = "Git"
    }
}

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "checkstyle"
    apply plugin: "findbugs"
    apply plugin: "pmd"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.gorylenko.gradle-git-properties"
    apply plugin: "groovy"
    apply plugin: "org.unbroken-dome.test-sets"

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
            mavenBom "com.google.inject:guice-bom:${google_guice_version}"
        }
        dependencies {
            dependency("cglib:cglib-nodep:3.2.4")
            dependency("commons-dbutils:commons-dbutils:1.6")
            dependency("com.amazon.redshift:redshift-jdbc42:1.2.1.1001")
            dependency("com.github.fge:json-patch:1.9")
            dependency("com.github.rholder:guava-retrying:2.0.0")
            // Guava even with guava.version set to 19.0 in properties keeps going back to 18.0 without this.
            dependency("com.google.guava:guava:19.0")
            dependencySet(group: "com.netflix.spectator", version: "0.55.0") {
                entry "spectator-api"
                entry "spectator-reg-servo"
            }
            dependencySet(group: "io.springfox", version: "2.7.0") {
                entry "springfox-swagger2"
                entry "springfox-swagger-ui"
                entry "springfox-bean-validators"
            }
            dependencySet(group: "io.swagger", version: "1.5.13") {
                entry "swagger-annotations"
                entry "swagger-jersey2-jaxrs"
            }
            dependency("de.danielbechler:java-object-diff:0.91.1")
            dependency("io.airlift:testing-mysql-server:0.1")
            dependency("javax.ws.rs:jsr311-api:1.1.1")
            dependency("net.sf.dozer:dozer:5.4.0")
            dependency("nl.jqno.equalsverifier:equalsverifier:1.7.2")
            dependency("org.apache.hadoop:hadoop-core:1.2.1")
            dependencySet(group: "org.apache.hive", version: "1.2.1") {
                entry "hive-metastore"
            }
            dependency("log4j:log4j:1.2.17")
            dependency("org.projectlombok:lombok:1.18.4")
            dependency("commons-logging:commons-logging:1.2")
            dependency("com.datastax.cassandra:cassandra-driver-core:3.6.0")
            dependency("org.json:json:20180813")
            dependency("com.google.code.findbugs:annotations:3.0.1")
            dependency("com.google.code.findbugs:jsr305:3.0.2")
            dependency("org.apache.logging.log4j:log4j-core:2.10.0")
            dependency("org.apache.pig:pig:0.14.0")
            dependency("org.codehaus.gpars:gpars:1.2.1")
            dependency("org.glassfish.jersey.containers:jersey-container-servlet:2.19")
            dependency("org.glassfish.jersey.media:jersey-media-json-jackson:2.19")
            dependencySet(group: "org.spockframework", version: "1.1-groovy-2.4") {
                entry("spock-core")
                entry("spock-guice")
                entry("spock-spring")
            }
            /**es 5.4.1 dependencies*/
            dependency("org.elasticsearch.client:transport:5.4.1")
            dependency("net.snowflake:snowflake-jdbc:3.4.2")
            dependency("com.esotericsoftware.kryo:kryo:2.22")
            dependency("com.github.Netflix.iceberg:iceberg-common:${iceberg_version}")
            dependency("com.github.Netflix.iceberg:iceberg-core:${iceberg_version}")
            dependency("com.github.Netflix.iceberg:iceberg-api:${iceberg_version}")
        }
    }

    configurations {
        all*.exclude module: "jcl-over-slf4j"
        all*.exclude module: "log4j-over-slf4j"
        all*.exclude module: "slf4j-jdk14"
        all*.exclude module: "slf4j-log4j12"
        all*.exclude module: "log4j-to-slf4j"
    }

    dependencies {

        /*******************************
         * Compile Dependencies
         *******************************/
        
        compile("log4j:log4j")

        /*******************************
         * Provided Dependencies
         *******************************/

        compileOnly("com.google.code.findbugs:annotations")
        compileOnly("com.google.code.findbugs:jsr305")
        
        /*******************************
         * Runtime Dependencies
         *******************************/

        /*******************************
         * Test Dependencies
         *******************************/

        testCompile("cglib:cglib-nodep")
        testCompile("org.codehaus.groovy:groovy-all")
        testCompile("org.codehaus.gpars:gpars")
        testCompile("org.objenesis:objenesis")
        testCompile("org.spockframework:spock-core")
        testCompile("org.spockframework:spock-guice")
        testCompile("org.spockframework:spock-spring")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testRuntime("org.hsqldb:hsqldb")
    }

    compileJava {
//        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        options.incremental = true
    }

    compileTestJava {
//        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    checkstyle {
        toolVersion = "6.11"
        configFile = new File(project.parent.projectDir, "codequality/checkstyle/checkstyle.xml")
    }

    findbugs {
        excludeFilter = new File(project.parent.projectDir, "codequality/findbugs/excludeFilter.xml")
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    // Print out full stack traces when our tests fail to assist debugging (e.g., when scanning Jenkins console output)
    tasks.withType(Test) {
        testLogging {
            exceptionFormat = "full"
        }
    }

    testSets {
        integrationTest { dirName = 'integration-test' }
    }

    pmdTest.enabled = false
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
